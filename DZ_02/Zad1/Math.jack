/**
 * A basic math library.
 */
class Math
{
    static Array twoToThe;

     function void init() {
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x)
    {
        if (x < 0)
        {
            return -x;
        }
        return x;
    }

    /** Returns the product of x and y. */
    function int multiply(int x, int y)
    {
        var int sum, shifted, i;
        let shifted = x;
        
        while (i < 16)
        {
            if (twoToThe[i] & y)
            {
                let sum = sum + shifted;
            }
            let shifted = shifted + shifted;
            let i = i + 1;
        }
        
        return sum;
    }

    /** Returns the integer part of x/y. */
    function int divide(int x, int y)
    {
        var int neg_x, neg_y;
        var int q;
        var int result;
        
        if( y > x ) {
            return 0;
        }

        let neg_x = x < 0;
        let neg_y = y < 0;
        let x = Math.abs(x);
        let y = Math.abs(y);

        
        let q = Math.divide(x, y+y);
        if( x-(2*q*y) < y ) {
            let result = q+q;
        }
        else {
            let result = q+q+1;
        }
        
        if( neg_x = neg_y ) {
            return result;
        }
        else {
            return -result;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x)
    {
        var int i, y, pow;
        
        if (x < 0)
        {
            return 0;
        }
        
        let i = 7;
        while (i > -1)
        {
            let pow = Math.multiply(y + twoToThe[i], y + twoToThe[i]);
            if (pow < x)
            {
                let y = y + twoToThe[i];
            }
            let i = i - 1;
        }
        
        return y;
    }

    /** Returns the greater number. */
    function int max(int x, int y)
    {
        if (x > y)
        {
            return x;
        }
        return y;
    }

    /** Returns the smaller number. */
    function int min(int x, int y)
    {
        if (x < y)
        {
            return x;
        }
        return y;
    }

    function int mod(int x, int y) {
        var int q;
        
        let q = Math.divide(x, y);
        return x - Math.multiply(q,y);
    }

    function int pow(int base, int power)
    {
        var int r;
        if (power = 0)
        {
            return 1;
        }

        if (base = 2){
            return twoToThe[power];
        }
        
        let r = Math.pow(base, Math.divide(power, 2));
        let r = Math.multiply(r, r);
        
        if (Math.mod(power, 2) = 1){
            return r * base;
        }
        return r;
    }

    function int log(int a)
    {
        var int i;
        if (a < 1)
        {
            return -1;
        }
        let i = 14;
        while(~(i < 0)) // >= 0
        {            
            if (~(twoToThe[i] > a)) // <=
            {
                return i;
            }
            let i = i - 1;
        }
        return -1;
    }
}